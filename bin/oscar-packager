#!/usr/bin/perl
#
# Copyright (c) 2008-2009 Oak Ridge National Laboratory
#                         Geoffroy Vallee <valleegr@ornl.gov>
#                         All rights reserved
#
# This file is part of the OSCAR software package.  For license
# information, see the COPYING file in the top level directory of the
# OSCAR source distribution.
#
# This script is a packager for OSCAR.
#
# $Id$
#

use strict;
use Getopt::Long;
use OSCAR::Utils;
use OSCAR::ConfigFile;
use OSCAR::OCA::OS_Detect;
use OSCAR::Logger;
use OSCAR::Packager;
use File::Path;
use Carp;
use warnings "all";

my ($core, $included, $verbose, $debug, $available_releases, $unsigned);

our $packaging_dir = "/tmp/oscar-packager";
use vars qw ($pkg_destdir);

# Getopt::Long::Configure("pass_through");
GetOptions(
        "core=s"                            => \$core,
        "included=s"                        => \$included,
        "supported-releases"                => \$available_releases,
        "unsigned-packages"                 => \$unsigned,
        "verbose"                           => \$verbose,
        "debug"                             => \$debug,
        "v"                                 => \$verbose,
        "help"                              => \&help_and_exit,
        ) || help_and_die();

sub pkgdir_cleanup () {
    if (-d $packaging_dir) {
        OSCAR::Logger::oscar_log_subsection "Removing $packaging_dir";
        File::Path::rmtree ($packaging_dir);
    }
    if (-d $packaging_dir) {
        carp "ERROR: Impossible to remove $packaging_dir";
        return -1;
    }
    mkdir ($packaging_dir);
    return 0;
}

# This function deals with prereqs for the packaging of a given OSCAR 
# component. The management of prereqs is based on a build.cfg file at the top
# directory of the OSCAR component source tree. If the file does not exist, we
# assume there is no prereq to deal with.
#
# Return: -1 if error, 0 else.
sub prepare_prereqs ($) {
    my $dir = shift;

    my $build_file = "$dir/build.cfg";
    if (! -f "$build_file") {
        OSCAR::Logger::oscar_log_subsection ("No $build_file, no prereqs");
    } else {
        OSCAR::Logger::oscar_log_subsection ("Managing prereqs ($build_file)");
        OSCAR::Packager::package_opkg ($build_file);
    }

    return 0;
}

sub create_package ($) {
    my $config_file = shift;
    my @failed_comp;

    #
    # First we check what is the underlying packaging system (RPM vs. Deb)
    #
    my $os = OSCAR::OCA::OS_Detect::open();
    if (!defined $os) {
        die "ERROR: Impossible to detect the binary package format";
    }

    #
    # We check that the destination directory for the packages is there, if not
    # we create it.
    #
    $pkg_destdir = "/tftpboot/oscar/";
    $pkg_destdir .= "$os->{distro}-$os->{distro_version}-$os->{arch}";
    if ( ! -d $pkg_destdir) {
        File::Path::mkpath ($pkg_destdir);
    }

    #
    # We clean-up the directory where we create the packages
    #
    pkgdir_cleanup ();

    #
    # We parse the config file.
    #
    OSCAR::Logger::oscar_log_subsection "Parsing $config_file...";
    my @cpts = OSCAR::ConfigFile::get_block_list ($config_file);
    OSCAR::Utils::print_array (@cpts) if $verbose;

    my $cmd;
    foreach my $c (@cpts) {
	OSCAR::Logger::oscar_log_subsection "Packaging $c...";
        my $source = OSCAR::ConfigFile::get_value ($config_file, $c, "source");
        my ($method, $url) = split (",", $source);
        if ($method eq "svn") {
            # We check out SVN
            $cmd = "cd $packaging_dir; svn co $url $c ";
            $cmd .= "1>/dev/null 2>/dev/null" if (!$debug);
            OSCAR::Logger::oscar_log_subsection "Executing: $cmd";
            system $cmd;

            # We check the prereqs (including dependencies with other packages)
            if (prepare_prereqs ("$packaging_dir/$c")) {
                carp "ERROR: Impossible to manage prereqs for the creation of ".
                     "the binary package";
                push (@failed_comp, $c);
            }

            # We compile
            $cmd = "cd $packaging_dir/$c; ";
            my $build_cmd = OSCAR::ConfigFile::get_value ($config_file,
                                                          $c,
                                                          "precommand");
            if (defined ($build_cmd)) {
                $cmd .= "$build_cmd && ";
            }
            if ($os->{pkg} eq "deb") {
                $cmd .= "make deb PKGDEST=$pkg_destdir ";
                $cmd .= "1>/dev/null 2>/dev/null" if (!$debug);
            } elsif ($os->{pkg} eq "rpm") {
                $cmd .= "make rpm PKGDEST=$pkg_destdir ";
                # On some fedora core releases, we cannot use /usr/src/redhat
                if (($os->{compat_distro} eq "fc") 
                    && ($os->{compat_distrover} >= 10)) {
                    $cmd .= " SOURCEDIR=~/rpmbuild/SOURCES ";
                }
                $cmd .= "1>/dev/null 2>/dev/null" if (!$debug);
            } else {
                die "ERROR: Unknow binary package format ($os->{pkg})";
            }
            OSCAR::Logger::oscar_log_subsection "Executing: $cmd";
            if (system ($cmd)) {
                OSCAR::Logger::oscar_log_subsection "[WARNING] Impossible to ".
                    "build $c ($cmd)";
                push (@failed_comp, $c);
            }
        } else {
            carp "ERROR: Unknow method to access source ($method)";
            push (@failed_comp, $c);
        }
    }

    OSCAR::Logger::oscar_log_subsection "[INFO] Generated binary packages ".
        "are in $packaging_dir";

    return @failed_comp;
}

sub help () {
    print "Please execute the 'man oscar-packager' command\n";
}

sub help_and_die {
    help ();
    exit 1;
}

sub help_and_exit {
    help ();
    exit 0;
}

my $config_file_path = "/etc/oscar/oscar-packager";
my $config_file;

# We create a log file specific to oscar-packager
my $log_file = "/var/log/oscar/oscar-packager.log";
OSCAR::Logger::init_log_file ($log_file);

# Now we correctly set OSCAR_VERBOSE to be sure we get all the output wee want
my $initial_verbose = $ENV{OSCAR_VERBOSE};
if ($verbose) {
    $ENV{OSCAR_VERBOSE} = 5;
}
if ($debug) {
    $ENV{OSCAR_VERBOSE} = 10;
}

$ENV{UNSIGNED_OSCAR_PKG} =1 if ($unsigned);

my @releases = OSCAR::Packager::available_releases();

#
# Special case: the user just want the list of available releases for which we
# can package OSCAR
#
if ($available_releases) {
    die "ERROR: Impossible to get the list of supported releases"
        if (scalar (@releases) == 0);
    print "Packaging capability available for the following OSCAR releases:\n";
    OSCAR::Utils::print_array (@releases);
    exit (0)
}

OSCAR::Logger::oscar_log_section ("OSCAR Packager Starting...");

if ($core && OSCAR::Utils::is_element_in_array($core, @releases) ) {
    $config_file = "$config_file_path/core_stable_$core.cfg";
    OSCAR::Logger::oscar_log_subsection ("Packaging stable OSCAR core");
}

if ($core && !OSCAR::Utils::is_element_in_array($core, @releases)) {
    $config_file = "$config_file_path/core_unstable.cfg";
    OSCAR::Logger::oscar_log_subsection ("Packaging unstable OSCAR core");
}

if ($included && OSCAR::Utils::is_element_in_array($included, @releases)) {
    $config_file = "$config_file_path/included_stable_$included.cfg";
    OSCAR::Logger::oscar_log_subsection ("Packaging stable included OPKGs");
}

if ($included && !OSCAR::Utils::is_element_in_array($included, @releases)) {
    $config_file = "$config_file_path/included_unstable.cfg";
    OSCAR::Logger::oscar_log_subsection ("Packaging unstable included OPKGs");
}

if (!defined $config_file) {
    die "ERROR: Invalid parameters";
}

if (! -f $config_file) {
    die "ERROR: Impossible to find the configuration file ($config_file)";
}
my @failed_comp = create_package ($config_file);
if (scalar (@failed_comp) > 0) {
    carp "Errors occured during the OSCAR packaging.\n".
          "OSCAR components that failed are: ";
    OSCAR::Utils::print_array (@failed_comp);
}

# Before to exit, we restore the initial OSCAR_VERBOSE value
if (defined $initial_verbose) {
    $ENV{OSCAR_VERBOSE} = $initial_verbose;
}

OSCAR::Logger::oscar_log_section ("OSCAR Packager Ending");

exit 0;

__END__

=head1 NAME

oscar-packager, a script for the creation of binary packages (Debian or RPM
packages) for the OSCAR core.
The script is based on a configuration file ('/etc/oscar/oscar-packager/'),
which includes different configuration files for different OSCAR release (i.e.,
stable, unstable).

=head1 SYNOPSIS

oscar-packager OPTIONS

=head1 OPTIONS

Recognized options include:

=over 8

=item --core [VERSION]

Package a given release of the OSCAR core. To package the development version, use "unstable" as VERSION.

=item --included [VERSION]

Package a given release of the OSCAR included OPKGs. To package the development version, use "unstable" as VERSION.

=item --supported-releases

Give the list of OSCAR releases that can be packaged.

=item --unsigned-packages

Do not sign the binary packages that will be created (currently only for Debian systems).

=item --verbose|-v

Fall back to the verbose mode of execution for debugging purpose.

=item --debug

Display even more output than when using the verbose option.

=back

=head1 AUTHOR

Geoffroy Vallee, Oak Ridge National Laboratory <valleegr at ornl dot gov>

=cut
